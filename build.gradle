plugins {
    id "java"
    id "java-library"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = "me.hannsi.lfjg"
version = "1.0-SNAPSHOT"

ext {
    lwjglVersion = "3.3.4"
    jomlVersion = "1.10.7"
    lwjgl3awtVersion = "0.1.8"
    steamworks4jVersion = "1.9.0"
    steamworks4jserverVersion = "1.9.0"
    jomlprimitivesVersion = "1.10.0"
    lwjglNatives = "natives-windows"
    javaVersion = 19
}

repositories {
    mavenCentral()
}

dependencies {
    // JOGL & GlueGen
    implementation 'org.jogamp.jogl:jogl-all-main:2.3.2'
    implementation 'org.jogamp.gluegen:gluegen-rt-main:2.3.2'

    // JNA
    implementation "net.java.dev.jna:jna:5.13.0"
    implementation "net.java.dev.jna:jna-platform:5.13.0"

    // Annotations
    api "org.jetbrains:annotations:24.0.0"

    // Testing
    testImplementation platform("org.junit:junit-bom:5.9.1")
    testImplementation "org.junit.jupiter:junit-jupiter"

    // Reflection
    api "org.reflections:reflections:0.10.2"

    // Logging
    api "org.apache.logging.log4j:log4j-api:2.23.1"
    api "org.apache.logging.log4j:log4j-core:2.23.1"
    api "org.apache.logging.log4j:log4j-web:2.23.1"
    api "org.slf4j:slf4j-api:1.7.36"
    api "ch.qos.logback:logback-classic:1.5.16"

    // XML graphics
    api("org.apache.xmlgraphics:xmlgraphics-commons:2.9") {
        exclude module: "commons-io"
    }
    api "commons-io:commons-io:2.14.0"
    api "org.apache.xmlgraphics:batik-transcoder:1.17"

    // LWJGL (BOM + modules)
    api platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    def lwjglModules = [
            "lwjgl", "lwjgl-assimp", "lwjgl-glfw", "lwjgl-nanovg",
            "lwjgl-openal", "lwjgl-opengl", "lwjgl-stb"
    ]
    lwjglModules.each { module ->
        implementation "org.lwjgl:$module"
        runtimeOnly "org.lwjgl:$module::$lwjglNatives"
    }

    // Math & AWT
    api "org.joml:joml:$jomlVersion"
    api "org.joml:joml-primitives:$jomlprimitivesVersion"
    api "org.lwjglx:lwjgl3-awt:$lwjgl3awtVersion"

    // Steamworks
    api "com.code-disaster.steamworks4j:steamworks4j:$steamworks4jVersion"
    api "com.code-disaster.steamworks4j:steamworks4j-server:$steamworks4jserverVersion"

    // JavaCV
    api "org.bytedeco:javacv-platform:1.5.10"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes "Main-Class": "me.hannsi.test.TestGuiFrame"
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

configurations {
    runtimeElements {
        extendsFrom runtimeOnly
    }
}