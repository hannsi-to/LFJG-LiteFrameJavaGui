plugins {
    id "java"
    id "java-library"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = "me.hannsi.lfjg.core"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    // Logging
    implementation "org.apache.logging.log4j:log4j-slf4j2-impl:2.23.1"
    implementation "org.apache.logging.log4j:log4j-core:2.23.1"
    annotationProcessor "org.apache.logging.log4j:log4j-core:2.23.1"

    if (lwjglVersion == 3) {
        // LWJGL BOM
        api platform("org.lwjgl:lwjgl-bom:$lwjgl3Version")

        //LWJGL
        api "org.lwjgl:lwjgl"
        runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"

        //STB
        api "org.lwjgl:lwjgl-stb"
        runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"

        // Math & AWT
        api "org.joml:joml:$jomlVersion"
        api "org.joml:joml-primitives:$jomlprimitivesVersion"
        api "org.lwjglx:lwjgl3-awt:$lwjgl3awtVersion"
    }

    // Reflection
    implementation "org.reflections:reflections:0.10.2"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:deprecation"
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

configurations {
    runtimeElements {
        extendsFrom runtimeOnly
    }
}

shadowJar {
    mergeServiceFiles()
}